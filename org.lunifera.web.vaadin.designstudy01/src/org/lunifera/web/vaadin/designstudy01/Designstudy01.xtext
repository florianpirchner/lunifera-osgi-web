grammar org.lunifera.web.vaadin.designstudy01.Designstudy01 with org.eclipse.xtext.common.Terminals

generate designstudy01 "http://www.lunifera.org/web/vaadin/designstudy01/Designstudy01"

UiModel:
	tiles=UiMasterDetailTiles;

UiMasterDetailTiles:
	{UiMasterDetailTiles}
	'MasterDetailTiles' name=ID '{'
	('layout' '=' layout=UiLayoutType)?
	((master=UiMasterPart)? &
	(detail=UiDetailPart)?)
	'}';

UiPart:
	UiMasterPart | UiDetailPart;

UiMasterPart:
	{UiMasterPart}
	'Master' '{'
	(content=UiSelectable)?
	'}';

UiDetailPart:
	{UiDetailPart}
	'Detail' '{'
	(('size' '=' layout=UiDetailSize)? &
	 ('text align' '=' textAlign = UiTextAlignment)?)
	(content+=UiSimpleField)*
	'}';

UiElement:
	UiVisuals;

UiVisuals:
	UiSelectable | UiSimpleField;

UiSelectable:
	UiTable;

UiSimpleField:
	UiTextField;

UiTable:
	'Table' name=ID_ESC caption=STRING ('binds to' bindsTo=BindsToEntity)?;

UiTextField:
	'TextField' name=ID_ESC caption=STRING ('binds to' bindsTo=BindsToProperty)?;

enum BindsToProperty:
	AGE_LIT='age' | LASTNAME='lastName' | FIRSTNAME='firstName';

enum BindsToEntity:
	PERSON='Person';

ID_ESC:
	ID | 'age' | 'firstName' | 'lastName' | 'Person';

LFQN:
	ID ('.' ID)*;

enum UiLayoutType:
	HORIZONTAL='horizontal' | VERTICAL='vertical';

enum UiDetailSize:
	BIG='big' | SMALL='small';
	
enum UiTextAlignment:
	FILL = "fill" | LEFT = "left" | CENTER = "center" | RIGHT = "right"
;