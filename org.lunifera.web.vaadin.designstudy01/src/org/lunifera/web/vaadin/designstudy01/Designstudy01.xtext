grammar org.lunifera.web.vaadin.designstudy01.Designstudy01 with org.eclipse.xtext.common.Terminals

generate designstudy01 "http://www.lunifera.org/web/vaadin/designstudy01/Designstudy01"
import "http://www.example.org/entitydsl/EntityDSL" as entity

UiModel:
	(imports+=Import)*
	tiles=UiMasterDetailTiles;

Import returns entity::LImport:
	('import' importedNamespace=QualifiedNameWithWildCard);

QualifiedNameWithWildCard:
	Fqn ('.' '*')?;

Fqn:
	ValidID (=> '.' ValidID)*;

ValidID:
	ID;

UiMasterDetailTiles:
	{UiMasterDetailTiles}
	'MasterDetailTiles' name=ID 'binds to' bindsTo=[entity::LEntity|Fqn] '{'
	('layout' '=' layout=UiLayoutType)?
	((master=UiMasterPart)? &
	(detail=UiDetailPart)?)
	'}';

UiPart:
	UiMasterPart | UiDetailPart;

UiMasterPart:
	{UiMasterPart}
	'Master' '{'
	(content=UiSelectable)?
	'}';

UiDetailPart:
	{UiDetailPart}
	'Detail' '{'
	(('size' '=' layout=UiDetailSize)? &
	('text align' '=' textAlign=UiTextAlignment)?)
	(content+=UiSimpleField)*
	'}';

UiElement:
	UiVisuals;

UiVisuals:
	UiSelectable | UiSimpleField;

UiSelectable:
	UiTable;

UiSimpleField:
	UiTextField;

UiTable:
	'Table' name=ID caption=STRING;

UiTextField:
	'TextField' name=ID caption=STRING ('binds to' bindsTo=[entity::LProperty|Fqn])?;

enum UiLayoutType:
	HORIZONTAL='horizontal' | VERTICAL='vertical';

enum UiDetailSize:
	BIG='big' | SMALL='small';

enum UiTextAlignment:
	FILL="fill" | LEFT="left" | CENTER="center" | RIGHT="right";